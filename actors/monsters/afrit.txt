actor Afrit2 : Afrit
{
	+STRIFEDAMAGE
	DropItem "HardMonsterDropOrClassDrop"
	DropItem "MediumMonsterDropOrClassDrop"
	DropItem "HealthBonus", 89
	DropItem "ArmorBonus", 89
	DropItem "Cell", 89
	DropItem "Clip", 89
	DropItem "Shell", 89
	DropItem "RocketAmmo", 89
	States
	  {
	  Spawn:
		FRIT A 0
		FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0, 2, 0)
		FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180, 2, 0)
		FRIT AABB 4 Bright A_Look
		Goto Spawn+3
	  Idle:
		FRIT ABCD 4 Bright A_Look
		Loop
	  Dodge: 			
		  FRIT ABCD 2 Bright A_Chase("Melee", "Missile", CHF_FASTCHASE)
		  TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000) 
		  TNT1 A 0 A_CheckLOF( "Missile", CLOFF_IGNOREGHOST, 2000 )
		  Goto See
	  See:
		TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000)
		FRIT AABBCCDD 2 Bright A_Chase
		Loop
	  Melee:
		TNT1 A 0 A_Jump(64, "Melee2")
		FRIT ST 6 Bright A_FaceTarget
		FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, 10, "Baron/Melee")
		Goto See
	  Melee2:
		FRIT E 6 Bright A_FaceTarget
		FRIT F 6 Bright A_CustomMissile("HellFire", 44)
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Missile:
		TNT1 A 0 A_Jump(256, "Missile0", "Missile1", "Missile2", "Missile3", "Missile4")
	  Missile0:
		FRIT ST 6 Bright A_FaceTarget
		FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, 10, "Baron/Melee")
		Goto See
	  Missile1:
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile("CurseBall", 44)
		Goto See
	  Missile2:
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile("Comet", 44)
		Goto See
	  Missile3:
		FRIT E 6 Bright A_FaceTarget
		FRIT F 6 Bright A_CustomMissile("Hellfire", 44)
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Missile4:
		FRIT E 6 Bright A_FaceTarget
		FRIT F 30 A_PlaySoundEx("DMC/Charge", "SoundSlot7", 0)
		FRIT G 4 Bright A_FaceTarget
		FRIT G 0 A_CheckLOF( "FireBlackHole", CLOFF_IGNOREGHOST, 1500 )
		Goto See
	  FireBlackHole:
		FRIT G 0 A_PlaySound("DMC/Fire")
		FRIT G 2 Bright A_CustomMissile("DMBall")
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Pain:
		FRIT H 4 Bright
		FRIT H 4 Bright A_Pain
		TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000) 
		TNT1 A 0 A_Jump(128, "Dodge")
		Goto See
	  Death:
		TNT1 A 0 A_NoGravity
		FRIT I 6 Bright A_Scream
		FRIT J 5 Bright A_NoBlocking
		FRIT KLMNOPQR 4 Bright
		Stop
	  }
}
 
