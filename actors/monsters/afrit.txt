actor Afrit2 : Afrit
{
	+STRIFEDAMAGE
	DropItem "HardMonsterDropOrClassDrop"
	DropItem "MediumMonsterDropOrClassDrop"
	DropItem "HealthBonus", 89
	DropItem "ArmorBonus", 89
	DropItem "Cell", 89
	DropItem "Clip", 89
	DropItem "Shell", 89
	DropItem "RocketAmmo", 89
	States
	  {
	  
	  Spawn:
		FRIT A 0
		FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0, 2, 0)
		FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180, 2, 0)
		
		//TNT1 A 0 A_GiveInventory ("BigBoost")
		FRIT AABB 4 Bright A_Look
		Goto Idle
	  Idle:
		FRIT AA 2 Bright A_Look
		FRIT AA 2 Bright A_Wander
		FRIT BB 2 Bright A_Look
		FRIT BB 2 Bright A_Wander
		FRIT CC 2 Bright A_Look
		FRIT CC 2 Bright A_Wander
		FRIT DD 2 Bright A_Look
		FRIT DD 2 Bright A_Wander
		Loop
	  Dodge: 			
		  FRIT ABCD 2 Bright A_Chase("Melee", "Missile", CHF_FASTCHASE)
		  TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000) 
		  TNT1 A 0 A_CheckLOF( "Missile", CLOFF_IGNOREGHOST, 2000 )
		  Goto See
	  See:
		TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000)
		FRIT AA 2 Bright A_Chase
		  TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000)
		FRIT BB 2 Bright A_Chase
		  TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000)
		FRIT CC 2 Bright A_Chase
		  TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000)
		FRIT DD 2 Bright A_Chase
		Loop
	  Melee:
		TNT1 A 0 A_CheckLOF( "Missile5", CLOFF_IGNOREGHOST, 1500 )
		TNT1 A 0 A_Jump(64, "Melee2")
		FRIT ST 6 Bright A_FaceTarget
		FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, 10, "Baron/Melee")
		Goto See
	  Melee2:
		FRIT E 6 Bright A_FaceTarget
		FRIT F 6 Bright A_CustomMissile("HellFire", 44)
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Missile:
		TNT1 A 0 A_Jump(256, "Missile0", "Missile1", "Missile2", "Missile3", "Missile4", "Missile5")
	  Missile0:
		TNT1 A 0 A_TakeInventory("RepeaterZombieHeat", 1)
		FRIT ST 6 Bright A_FaceTarget
		FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, 10, "Baron/Melee")
		Goto See
	  Missile1:
		TNT1 A 0 A_TakeInventory("RepeaterZombieHeat", 1)
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile("CurseBall", 44)
		Goto See
	  Missile2:
		TNT1 A 0 A_TakeInventory("RepeaterZombieHeat", 1)
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile("Comet", 44)
		Goto See
	  Missile3:
		TNT1 A 0 A_TakeInventory("RepeaterZombieHeat", 1)
		FRIT E 6 Bright A_FaceTarget
		FRIT F 6 Bright A_CustomMissile("Hellfire", 44)
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Missile4:
		TNT1 A 0 A_JumpIfInventory("RepeaterZombieHeat", 1, "Missile")
		FRIT E 6 Bright A_FaceTarget
		FRIT F 30 A_PlaySoundEx("DMC/Charge", "SoundSlot7", 0)
		FRIT G 4 Bright A_FaceTarget
		FRIT G 0 A_CheckLOF( "FireBlackHole", CLOFF_IGNOREGHOST, 1500 )
		Goto See
	   Missile5:
		FRIT E 2 Bright A_FaceTarget
	    FRIT EFG 3 A_CustomMissile("plasmaball",32,10,0,1) // Class, spawn height, offset, angle, aimmode
		TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000) 
		TNT1 A 0 A_Jump(32, "See")
		FRIT G 1 A_MonsterRefire(130, "See") // About 50% chance to jump to "See" if target is out of sight.
		TNT1 A 0 A_CheckLOF("Missile5", CLOFF_IGNOREGHOST, 2000 )
		Goto Missile5+1                  // Looping back to the attack state to allow the actual refire!
	  FireBlackHole:
		FRIT G 0 A_PlaySound("DMC/Fire")
		FRIT G 2 Bright A_CustomMissile("DMBall")
		TNT1 A 0 A_GiveInventory("RepeaterZombieHeat", 4)
		FRIT G 6 Bright A_FaceTarget
		Goto See
	  Pain:
		FRIT H 4 Bright
		FRIT H 4 Bright A_Pain
		TNT1 A 0 A_JumpIfInTargetLOS("Dodge", 20, JLOSF_DEADNOJUMP, 1000) 
		TNT1 A 0 A_Jump(128, "Dodge")
		Goto See
	  }
}
 
actor TestInvisibility : PowerupGiver 84
{
  inventory.maxamount 0
  powerup.duration -5
  powerup.type "Invisibility"
  +COUNTITEM
  +FLOATBOB
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  states
  {
  Spawn:
    INVU ABCD 3
    loop
	
	  Death:
	  
	TNT1 A 35 A_PrintBold("Whoa")
	stop
  }
}